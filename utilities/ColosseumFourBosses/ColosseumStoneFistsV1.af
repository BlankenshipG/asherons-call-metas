~~ {
~~ FOR AUTO-COMPLETION ASSISTANCE: testvar getvar setvar touchvar clearallvars clearvar getcharintprop getchardoubleprop getcharquadprop getcharboolprop getcharstringprop getisspellknown getcancastspell_hunt getcancastspell_buff getcharvital_base getcharvital_current getcharvital_buffedmax getcharskill_traininglevel getcharskill_base getcharskill_buffed getplayerlandcell getplayercoordinates coordinategetns coordinategetwe coordinategetz coordinatetostring coordinateparse coordinatedistancewithz coordinatedistanceflat wobjectgetphysicscoordinates wobjectgetname wobjectgetobjectclass wobjectgettemplatetype wobjectgetisdooropen wobjectfindnearestmonster wobjectfindnearestdoor wobjectfindnearestbyobjectclass wobjectfindininventorybytemplatetype wobjectfindininventorybyname wobjectfindininventorybynamerx wobjectgetselection wobjectgetplayer wobjectfindnearestbynameandobjectclass actiontryselect actiontryuseitem actiontryapplyitem actiontrygiveitem actiontryequipanywand actiontrycastbyid actiontrycastbyidontarget chatbox chatboxpaste statushud statushudcolored uigetcontrol uisetlabel isfalse istrue iif randint cstr strlen getobjectinternaltype cstrf stopwatchcreate stopwatchstart stopwatchstop stopwatchelapsedseconds cnumber floor ceiling round abs getworldname getitemcountininventorybyname getheading getitemcountininventorybynamerx getheadingto actiontrygiveprofile vitae getfellowshipstatus getfellowshipname getfellowshipisopen getfellowshipisleader getfellowshipleaderid getfellowshipcanrecruit getfellowid getfellowshipcount getfellowshiplocked getfellowname getfellowshipisfull sin cos tan sqrt asin acos atan atan2 sinh cosh tanh vtsetmetastate getregexmatch echo chr ord wobjectgetid wobjectgethealth wobjectfindbyid wobjectgetintprop wobjectfindnearestbytemplatetype wobjectgetopencontainer testquestflag getquestktprogress isrefreshingquests getquestktrequired getqueststatus getisday getgamehour getgamehourname getisnight getgameday getgameticks getminutesuntilday getgamemonth getgamemonthname getminutesuntilnight getgameyear uisetvisible uiviewvisible uiviewexists getgvar touchgvar getpvar touchpvar setgvar cleargvar setpvar clearpvar testgvar clearallgvars testpvar clearallpvars dictgetitem dictcreate dicthaskey dictadditem dictkeys dictremovekey dictvalues dictclear dictsize dictcopy listgetitem listpop listcreate listcontains listremove listadd listindexof listremoveat listinsert listlastindexof listclear listcopy listcount listreverse

~~																						
~~ File auto-generated by metaf, a program created by Eskarina of Morningthaw/Coldeve.	
~~		Get metaf here: https://github.com/JJEII/metaf/									
~~																						
~~ All recognized structural designators:												
~~		STATE:				DO:															
~~		IF:					NAV:														
~~																						
~~ All recognized CONDITION (IF:) operation keywords:									
~~		Never				NavEmpty			MobsInDist_Priority		Not				
~~		Always				Death				NeedToBuff				PSecsInStateGE	
~~		All					VendorOpen			NoMobsInDist			SecsOnSpellGE	
~~		Any					VendorClosed		BlockE					BuPercentGE		
~~		ChatMatch			ItemCountLE			CellE					DistToRteGE		
~~		MainSlotsLE			ItemCountGE			IntoPortal				Expr			
~~		SecsInStateGE		MobsInDist_Name		ExitPortal				ChatCapture		
~~																						
~~ All recognized ACTION (DO:) operation keywords:										
~~		None				EmbedNav			ChatExpr				SetOpt			
~~		SetState			CallState			SetWatchdog				CreateView		
~~		Chat				Return				ClearWatchdog			DestroyView		
~~		DoAll				DoExpr				GetOpt					DestroyAllViews	
~~																						
~~ All recognized NAV types:															
~~		circular			follow														
~~		linear				once														
~~																						
~~ All recognized NAV NODE types:														
~~		flw					vnd															
~~		pnt					ptl															
~~		rcl					tlk															
~~		pau					chk															
~~		cht					jmp															
~~		prt (deprecated in VTank)														
~~ }																					

STATE: {Default} ~~ {
	IF: Expr {getqueststatus[pickedupstonefiststoken]==0}
		DO: DoAll
				DoExpr {echo[Colosseum Stone Fists timer not up\!,15]}
				SetState {End}
	IF:	Expr {getqueststatus[pickedupstonefiststoken]!=0}
		DO:	SetState {Settings}		
~~ }
STATE: {Settings} ~~ {
	IF:	Always
		DO:	DoAll
				Chat {/ub opt set VTank.PatchExpressionEngine True}
				DoExpr {vtsetsetting[NavCloseStopRange,`0.00625`]} ~~ 1.5
				DoExpr {vtsetsetting[NavPriorityBoost,`0`]}
				DoExpr {vtsetsetting[EnableBuffing,`0`]}
				DoExpr {vtsetsetting[EnableCombat,`0`]}
				DoExpr {vtsetsetting[EnableNav,`1`]}
				DoExpr {vtsetsetting[IdlePeaceMode,`1`]}
				DoExpr {vtsetsetting[EnableLooting,`1`]}
				DoExpr {vtsetsetting[LootOnlyRareCorpses,`1`]}
				DoExpr {uboptset[`VTank.FixPortalLoops`,True]}
				DoExpr {uboptset[`Looter.Enabled`,True]}
				DoExpr {uboptset[`Looter.EnableChests`,True]}
				DoExpr {uboptset[`VTank.VitalSharing`,True]}	
				DoExpr {vtsetsetting[UseDispelItems,`0`]}
				DoExpr {vtsetsetting[AutoAttackPower,`1`]}
				DoExpr {vtsetsetting[BlacklistMonsterTimeoutSeconds,`30`]}
				DoExpr {vtsetsetting[Recharge-Norm-Hitp,`70`]}
				DoExpr {vtsetsetting[Recharge-Norm-Stam,`5`]}
				DoExpr {vtsetsetting[Recharge-Norm-Mana,`15`]}
				DoExpr {vtsetsetting[Recharge-Notarg-Hitp,`0`]}
				DoExpr {vtsetsetting[Recharge-Notarg-Stam,`0`]}
				DoExpr {vtsetsetting[Recharge-Notarg-Mana,`50`]}
				DoExpr {vtsetsetting[OpenDoors,`1`]} 
				DoExpr {vtsetsetting[SummonPets,`0`]}
				DoExpr {vtsetsetting[PetRangeMode,`1`]}
				DoExpr {vtsetsetting[PetCustomRange,`0.0625`]} ~~ 15
				DoExpr {vtsetsetting[AttackDistance,`0.25`]} ~~ 60 to hit anything in the room
				DoExpr {vtsetsetting[ApproachDistance,`0.41666`]} ~~ loot any in the room
				Chat {/vt loot load ColosseumBosses.utl}
	IF: ItemCountGE 1 {Egg}
		DO: DoExpr {setgvar[groupCounter,0]}
	IF: SecsInStateGE 3
		DO: SetState {Begin}				
~~ }
STATE: {Begin} ~~ {
	IF: Always
		DO: DoAll
				DoExpr {echo[Colosseum Stone Fists Meta by Sun Invictus,15]}
				DoExpr {echo[Must be flagged for Stone Fists and have Colosseum Recall,15]}
	IF: SecsInStateGE 10
		DO: DoAll
				DoExpr {setvar[stopWatch,stopwatchstart[stopwatchcreate[]]]}
				CallState {CheckForColosseum} {NavigatingToStoneFists}
~~ }
STATE: {CheckForColosseum} ~~ {
	IF: Any
			CellE 00AF0118 ~~ colo drop
			CellE 00AF0117 ~~ next to Master Arbitrator
		DO: Return
	IF: All
			Not CellE 00AF0118
			Not CellE 00AF0117
		DO: SetState {ColosseumRecall}
~~ }
STATE: {ColosseumRecall} ~~ {
	IF: Always
		DO: EmbedNav nav0 {ColosseumRecall.nav}
	IF: All
			ExitPortal
			BlockE 00AF0000
		DO: SetState {CheckForColosseum}
~~ }
STATE: {NavigatingToStoneFists} ~~ {
	IF: Always
		DO: EmbedNav nav1 {ToStoneFists.nav}
	IF:	All
			NavEmpty
			SecsInStateGE 3
		DO:	SetState {EnterStoneFists}
~~ }
STATE: {EnterStoneFists} ~~ {
	IF: Always
		DO: Chat {/ub usel Stone Fists}
	IF: IntoPortal
		DO: DoAll
				DoExpr {vtsetsetting[LootOnlyRareCorpses,`0`]}
				DoExpr {vtsetsetting[EnableCombat,`1`]}
				SetState {LootStoneFists}
~~ }
STATE: {LootStoneFists} ~~ {
	IF: All
			ItemCountGE 1 {Stone Fists Token}
			Expr {getfellowshipstatus[]==0}
		DO: DoAll
				DoExpr {vtsetsetting[LootOnlyRareCorpses,`1`]}
				CallState {CheckForColosseum} {GiveStoneFistsToken}
	IF: All
			ItemCountGE 1 {Stone Fists Token}
			Expr {getfellowshipstatus[]==1}
		DO: DoAll
				DoExpr {vtsetsetting[LootOnlyRareCorpses,`1`]}
				DoExpr {setgvar[groupCounter,getgvar[groupCounter]+1]}
				SetState {WaitingForGroup}
~~ }
STATE: {WaitingForGroup} ~~ {
	IF: Expr {getgvar[groupCounter]==getfellowshipcount[]}
		DO: CallState {CheckForColosseum} {GiveStoneFistsToken}
~~ }
STATE: {GiveStoneFistsToken} ~~ {
	IF: Always
		DO:	Chat {/ub give Stone Fists Token to Master Arbitrator}
	IF: All
			ItemCountLE 0 {Stone Fists Token}
			ItemCountGE 1 {Fists of Stone}
		DO: SetState {DumpFistsOfStone}
	IF: SecsInStateGE 5
		DO: SetState {GiveStoneFistsToken}
~~ }
STATE: {DumpFistsOfStone} ~~ {
	IF: Always
		DO:	Chat {/ub give Fists of Stone to Garbage Barrel}
	IF: ItemCountLE 0 {Fists of Stone}
		DO: SetState {End}
	IF: SecsInStateGE 5
		DO: SetState {DumpFistsOfStone}
~~ }
STATE: {End} ~~ {
	IF:	Always 
		DO:	DoAll
				DoExpr {stopwatchstop[getvar[stopWatch]]}
				Chat {/vt loot load DefaultLoot.utl}
	IF: Expr {getfellowshipstatus[]==1}
		DO: ChatExpr {\/f Colosseum\ Boss\ Stone\ Fists\ +cstr[floor[stopwatchelapsedseconds[getvar[stopWatch]]/60]]+\ Minutes\ +cstr[floor[stopwatchelapsedseconds[getvar[stopWatch]]%60]]+\ Seconds]}
	IF: All 
			SecsInStateGE 3
			Expr {testvar[fourColoBosses]==1}
		DO: Chat {/vt meta load ColosseumAzaxisV1}
	IF: All 
			SecsInStateGE 3
			Expr {testvar[fourColoBosses]==0}
			Expr {testgvar[defaultMeta]==1}
		DO: ChatExpr {\/vt meta load +getgvar[defaultMeta]}
~~ }

~~========================= ONLY NAVS APPEAR BELOW THIS LINE =========================~~

NAV: nav0 once ~~ {
	rcl -101.372306251526 45.1332907040914 -0.0749791701634725 {Colosseum Recall}
~~ }
NAV: nav1 once ~~ {
	pnt -101.783027919134 37.9263733307521 2.08333134651184E-05
~~ }

